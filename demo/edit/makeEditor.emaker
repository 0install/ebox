def <swt> := <import:org.eclipse.swt.*>
def <widget> := <swt:widgets.*>
def SWT := <swt:makeSWT>
def <swttools> := <import:org.erights.e.ui.swt.*>
def swtGrid__quasiParser := <swttools:swtGridQParserMaker>()

def makeEditor(powerbox, var myFile) {
	def shell := powerbox.makeShell()
	shell.open()

	var modified := false

	def textArea := <widget:makeText>(shell.getPanel(), SWT.getMULTI())

	if (myFile != null) {
		textArea.setText(myFile.getText())
	}

	def updateTitle() {
		var title := if (myFile == null) {
			"<untitled>"
		} else {
			myFile.getPlatformPath()
		}
		if (modified) {
			title += " *"
		}
		shell.setText(title)
	}
	updateTitle()

	textArea.addModifyListener(def _ {
		to modifyText(ev) {
			if (!modified) {
				modified := true
				updateTitle()
			}
		}
	})

	def importExport {
		to open(file) {
			if (myFile == null && textArea.getText() == "") {
				textArea.setText(file.getText())
				myFile := file
				updateTitle()
			} else {
				makeEditor(powerbox, file)
			}
		}

		to getDefaultFilename() {
			if (myFile == null) {
				return "textfile.txt"
			} else {
				return myFile
			}
		}

		to saveAs(file) {
			file.setText(textArea.getText())
			myFile := file
			modified := false
			updateTitle()
		}
	}

	def fileMenu := shell.addMenu("&File")

	def newItem := <widget:makeMenuItem>(fileMenu, SWT.getPUSH())
	newItem.setText("&New")
	newItem.addSelectionListener(def _ {
		to widgetSelected(ev) {
			makeEditor(powerbox, null)
		}
	})

	def openItem := <widget:makeMenuItem>(fileMenu, SWT.getPUSH())
	openItem.setText("&Open")
	openItem.addSelectionListener(def _ {
		to widgetSelected(ev) {
			shell.openFile(importExport)
		}
	})

	def saveItem := <widget:makeMenuItem>(fileMenu, SWT.getPUSH())
	saveItem.setText("&Save")
	saveItem.addSelectionListener(def _ {
		to widgetSelected(ev) {
			if (myFile == null || !myFile.canWrite()) {
				shell.saveFile(importExport)
			} else {
				importExport.saveAs(myFile)
			}
		}
	})

	def saveAsItem := <widget:makeMenuItem>(fileMenu, SWT.getPUSH())
	saveAsItem.setText("Save &As...")
	saveAsItem.addSelectionListener(def _ {
		to widgetSelected(ev) {
			shell.saveFile(importExport)
		}
	})

	def helpMenu := shell.addMenu("&Help")

	def manualItem := <widget:makeMenuItem>(helpMenu, SWT.getPUSH())
	manualItem.setText("&User Guide")
	manualItem.addSelectionListener(def _ {
		to widgetSelected(ev) {
			def shell := powerbox.makeShell()
			shell.setText("User Guide")
			shell.open()
			<help:makeHelp>(shell, <this:manual>)
		}
	})

	def mainArea := shell.getPanel()

	swtGrid`$mainArea: $textArea.X.Y`
	mainArea.getLayout().setMarginWidth(0)
	mainArea.getLayout().setMarginHeight(0)
}
